beancount_file = { SOI ~ (directive | blank_line | comment_line)* ~ EOI }

/// Grey
directive = _{ (transaction | open | close) ~ space* ~ inline_comment? ~ (NEWLINE | EOI) }
open = { date ~ space+ ~ "open" ~ space+ ~ account ~ space* ~ currency_list? }
close = { date ~ space+ ~ "close" ~ space+ ~ account }
currency_list = _{ currency ~ (comma_separator ~ currency)* }

transaction = { date ~ space+ ~ (transaction_flag | "txn") ~ (space+ ~ payee_and_description)? }
transaction_flag = _{ "*" | "!" }
payee_and_description = { string ~ (space+ ~ string)? }

/// Green
string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

currency = { ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "'" | "-" | "." | "_")* }

/// Yellow
account = { account_type ~ (":" ~ account_component)* }
account_type = { "Assets" | "Liabilities" | "Equity" | "Income" | "Expenses" }
account_component = { (ASCII_ALPHANUMERIC | "-")+ }

/// Blue
date = { year ~ "-" ~ month ~ "-" ~ day }
year = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }

blank_line = _{ !EOI ~ space* ~ (NEWLINE | EOI) }
comment_line = _{ !(date | EOI) ~ comment ~ (NEWLINE | EOI) }
inline_comment = _{ ";"+ ~ comment }
comment = _{ (!NEWLINE ~ ANY)* }

space = _{ (" " | "\t")+ }
comma_separator = _{ space* ~ "," ~ space* }
