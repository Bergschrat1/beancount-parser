beancount_file = { SOI ~ (directive | blank_line | comment_line)* ~ EOI }

directive = _{ (transaction | open | close) ~ space* ~ inline_comment? ~ (NEWLINE | EOI) }
open = { date ~ space+ ~ "open" ~ space+ ~ account ~ space* ~ currency_list? }
close = { date ~ space+ ~ "close" ~ space+ ~ account }
currency_list = _{ currency ~ (comma_separator ~ currency)* }

/// Blue
transaction =
    { date
    ~ space+
    ~ (transaction_flag | "txn")
    ~ payee_and_narration?
    ~ postings
    }
transaction_flag = { "*" | "!" }
payee_and_narration = _{ space+ ~ (payee ~ space+ ~ narration | narration) }
payee = { string }
narration = { string }
string = _{ "\"" ~ string_content ~  "\"" }
string_content = { (!"\"" ~ ANY)* }

postings = { (NEWLINE ~ posting)* }
posting = { space* ~ account ~ (space+ ~ amount)? ~ space* }

amount = { expression ~ space+ ~ currency }
expression = { value }
value = { ("-" | "+")? ~ ASCII_DIGIT* ~ ("." ~ ASCII_DIGIT+)? }
currency = { ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "'" | "-" | "." | "_")* }

account = { account_type ~ (":" ~ account_component)* }
account_type = { "Assets" | "Liabilities" | "Equity" | "Income" | "Expenses" }
account_component = { (ASCII_ALPHANUMERIC | "-")+ }

date = { year ~ "-" ~ month ~ "-" ~ day }
year = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }

blank_line = _{ !EOI ~ space* ~ (NEWLINE | EOI) }
comment_line = _{ space* ~ !(date | (account_type ~ ":") | EOI) ~ comment ~ (NEWLINE | EOI) }
inline_comment = _{ ";"+ ~ comment }
comment = _{ (!NEWLINE ~ ANY)* }

space = _{ " " | "\t" }
comma_separator = _{ space* ~ "," ~ space* }
